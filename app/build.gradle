apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        println "===================error code!"
        return 1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        println "===================error name!"
        return "1.0.0";
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    flavorDimensions 'internal'
    defaultConfig {
        applicationId "com.scrat.gogo"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode getVersionCode()
        versionName getVersionName() + "." + getVersionCode()
        manifestPlaceholders = [CHANNEL_NAME: "gogo", UMENG_APPKEY: UMENG_APPKEY]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            try {
                signingConfig signingConfigs.config
            } catch (ignore) {
            }
        }
        debug {
            try {
                signingConfig signingConfigs.config
            } catch (ignore) {
            }
        }
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        gogo {
            dimension 'internal'
            manifestPlaceholders = [CHANNEL_NAME: "gogo"]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.+'
    compile 'com.android.support:design:26.+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'

    compile 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'

    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.google.code.gson:gson:2.7'

    compile files('libs/alipaySdk-20170922.jar')

    compile 'com.github.bumptech.glide:glide:4.3.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.3.0'

    compile 'com.qiniu:qiniu-android-sdk:7.3.8'

    compile 'org.apmem.tools:layouts:1.10@aar'

    compile files('libs/open_sdk_r5886_lite.jar')

    compile 'com.facebook.shimmer:shimmer:0.1.0@aar'

    compile 'com.umeng.analytics:analytics:latest.integration'
}
